import com.avast.gradle.dockercompose.ContainerInfo
import com.avast.gradle.dockercompose.tasks.ComposeDown
import com.avast.gradle.dockercompose.tasks.ComposeUp
import com.bmuschko.gradle.docker.tasks.container.DockerWaitContainer

import java.util.concurrent.Callable

plugins {
    id "eu.xenit.docker" version "5.1.1" apply false
    id "eu.xenit.docker-compose" version "5.1.1" apply false
}

static String getBranchName(Project project) {
    return project.hasProperty('branchName') ? project.branchName : (System.getenv('BRANCH_NAME') ?: 'local')
}

static String getTagQualifier(String branchName) {
    if (branchName.endsWith('release')) {
        return ''
    }

    if (branchName.endsWith('master')) {
        return '-SNAPSHOT'
    }

    return '-' + branchName.split('/').last() + '-SNAPSHOT'
}

static List<String> calcTags(Project project) {
    def java = project.java
    def os = project.os
    def extraTags = project.hasProperty('extraTags') ? project.extraTags : []

    def javaTags = []
    javaTags << "${java.flavor}-${java.version.major}u${java.version.update}"
    javaTags << "${java.flavor}-${java.version.major}"

    def osTags = []
    osTags << "${os.vendor}-${os.version}"
    if (os.name) osTags << os.name
    if (isDefaultOsVersion(os)) osTags << os.vendor

    // create every java- & os-tag combination
    def tags = [javaTags, osTags]
            .combinations()
            .collect { it.join('-') }

    // add another tag that omits distro-information if
    // it's our default OS release (currently Ubuntu 16.04)
    if (isDefaultOsRelease(os)) {
        tags += javaTags
    }

    def allTags = extraTags + tags
    def branchName = getBranchName(project)
    def tagsWithQualifier = allTags.collect { it + getTagQualifier(branchName) }

    if (!branchName.endsWith('release')) {
        return tagsWithQualifier
    }
    return allTags + tagsWithQualifier
}

ext {
    ADOPTOPENJDK = [
            8 : [major: 8, update: 275, extra: 'b01', esum: '06fb04075ed503013beb12ab87963b2ca36abe9e397a0c298a57c1d822467c29'],
            11: [major: 11, update: 9, extra: '11', esum: 'fc8af86bc0cff91e1f96860d22a9611fdaa74a53d68f88ffb887d998593370a1']
    ]

    ORACLEOPENJDK = [
            7: [
                    'centos': [major: 7, update: 261],
                    'ubuntu': [major: 7, update: 211]
            ]
    ]
}


static Map getDefaultOsVersion(vendor) {
    [
            [vendor: 'ubuntu', version: '18.04'],
            [vendor: 'centos', version: '7']
    ].find { it.vendor == vendor }
}

static boolean isDefaultOsVersion(os) {
    def defaultRelease = getDefaultOsVersion(os.vendor)
    return defaultRelease.version == os.version
}

static boolean isDefaultOsRelease(os) {
    return os.vendor == 'ubuntu' && isDefaultOsVersion(os)
}

static ContainerInfo getFirstContainer(String serviceName, ComposeUp composeTask) {
    composeTask.servicesInfos.get(serviceName).firstContainer
}

subprojects { Project project ->

    repositories {
        mavenCentral()
    }

    project.projectDir.mkdir()

    apply plugin: 'eu.xenit.docker'
    apply plugin: 'eu.xenit.docker-compose'
    apply plugin: 'base'

    apply from: "${project.projectDir}.gradle"

    task copyDocker(type: Copy) {
        if (new File("$project.parent.projectDir/src/main/docker/Dockerfile.${project.os.vendor}.${project.java.vendor}").exists()) {
            from("$project.parent.projectDir/src/main/docker") {
                include "Dockerfile.${project.os.vendor}.${project.java.vendor}"
                rename "Dockerfile.${project.os.vendor}.${project.java.vendor}", 'Dockerfile'

                include 'docker-entrypoint.sh'
                include 'docker-entrypoint.d/'
            }
            into "${project.buildDir}/docker"
        }
    }

    buildDockerImage.dependsOn(copyDocker)

    dockerBuild {
        dockerFile = file("${project.buildDir}/docker/Dockerfile")
        repositories = ["docker.io/xenit/openjdk"]
        tags = calcTags(project)
    }

    buildDockerImage {
        buildArgs = [
                'BASE_IMAGE'         : "${project.os.vendor}:${project.os.version}",

                'JAVA_FLAVOR'        : project.java.flavor,
                'JAVA_VERSION_MAJOR' : "${project.java.version.major}",
                'JAVA_VERSION_UPDATE': "${project.java.version.update}"
        ]

        if (project.java.version.get('extra')) {
            if (java.version.major < 11) {
                buildArgs.put('JAVA_VERSION', "${project.java.version.major}u${project.java.version.update}-${project.java.version.extra}");
                buildArgs.put('JAVA_VERSION_COMPACT', "${project.java.version.major}u${project.java.version.update}${project.java.version.extra}");
            } else {
                buildArgs.put('JAVA_VERSION', "-${project.java.version.major}.0.${project.java.version.update}+${project.java.version.extra}");
                buildArgs.put('JAVA_VERSION_COMPACT', "${project.java.version.major}.0.${project.java.version.update}_${project.java.version.extra}");
            }
        }

        if (project.java.version.get('esum')) {
            buildArgs.put('JAVA_VERSION_ESUM', "${project.java.version.esum}")
        }

        buildArgs.putAll(project.findProperty('extraBuildArgs') ?: [:])

        doFirst {
            println buildArgs
        }
    }

    clean.doLast {
        project.projectDir.delete()
    }

    docker {
        registryCredentials {
            username = System.getenv("DOCKER_USER")
            password = System.getenv("DOCKER_PASSWORD")
        }
    }

    dockerCompose {
        captureContainersOutput = true

        goss {
            useComposeFiles = [
                    "${project.parent.projectDir}/src/test/goss/docker-compose.yml"
            ]

            if (project.hasProperty('extraBuildArgs') && project.extraBuildArgs.get('JAVA_VERSION_REPORTED'))
                environment.put 'GOSS_JAVA_VERSION', project.extraBuildArgs.get('JAVA_VERSION_REPORTED')
            else if (project.java.version.major > 8)
                environment.put 'GOSS_JAVA_VERSION', "${project.java.version.major}.0.${project.java.version.update}"
            else
                environment.put 'GOSS_JAVA_VERSION', "1.${project.java.version.major}.0_${project.java.version.update}"

            environment.put 'GOSS_OS_VENDOR', "${project.os.vendor}"
            environment.put 'GOSS_OS_VERSION', "${project.os.version}"

            environment.put 'JAVA_OPTS_GOSS', "-Dtest.java.opts=goss-test"
        }

        basic {
            useComposeFiles = [
                    "${project.parent.projectDir}/src/test/compose/docker-compose.yml"
            ]
        }
    }

    // Create a task that runts the compose-configuration and verifies
    // that the java container exits with status code 0
    def createComposeTestTask = { String config ->
        String taskname = "test${config.capitalize()}ComposeUp"

        def waitTask = project.tasks.create(taskname, DockerWaitContainer.class) { DockerWaitContainer task1 ->
            group = 'verification'

            ComposeUp composeUp = project.tasks["${config}ComposeUp"]
            task1.dependsOn(composeUp)
            task1.targetContainerId(new Callable<String>() {
                @Override
                String call() throws Exception {
                    ContainerInfo javaContainer = getFirstContainer('java', composeUp)

                    return javaContainer.getContainerId()
                }
            })
            onNext { r ->
                ContainerInfo container = getFirstContainer('java', composeUp)
                println "Container ${container} exited (exit code '${r.statusCode}'"
                if (r.statusCode) {

                    throw new GradleException("Docker image test failed: ${container.inspection.Config.Image}")
                }
            }
        }
        ComposeDown composeDown = project.tasks["${config}ComposeDown"]
        project.tasks.composeBasedImageTests.finalizedBy(composeDown)
        project.tasks.composeDownAll.dependsOn(composeDown)
        return waitTask
    }

    task composeDownAll {
    }

    task composeBasedImageTests {
        group = 'verification'

        ['basic', 'goss'].each {
            Task test = createComposeTestTask(it)
            dependsOn test
        }
    }

    check {
        dependsOn(composeBasedImageTests)
    }

    // TODO this can be removed once DEVEM-343 is resolved
    buildDockerImage {
        doLast {
            project.tasks.withType(ComposeUp.class) { ComposeUp composeUp ->
                composeUp.settings.environment.put("DOCKER_IMAGE", buildDockerImage.getImageId())
            }
        }
    }

    // All ComposeUp tasks should depend on `buildDockerImage` in our case ?
    project.tasks.withType(ComposeUp.class) { ComposeUp composeTask ->
        composeTask.dependsOn(project.tasks.buildDockerImage)
    }

}

task printSupportedTags {
    doFirst {

        def currentTags = (subprojects).collect { calcTags(it) }

        println '## Supported tags'
        println ''

        currentTags.each { tags ->
            println "* ${tags.collect { "`$it`" }.join(', ')}"
        }
    }
}

task printTags {
    doFirst {
        project.subprojects.collect().forEach { p ->
            println "* ${calcTags(p).reverse().collect { tag -> "`${tag}`" }.join(', ')}"
        }

    }
}

task verifyImageTags(group: 'verification') {
    doLast {
        // allTags is a map from tag -> [ projects ]
        def allTags = subprojects.collectEntries {
            [(it): calcTags(it)]
        }
                .collectMany { project, tags ->
                    tags.collect { new MapEntry(it, project) }
                }
                .groupBy { it.key.toString() }
                .collectEntries { [(it.key), it.value.collect { entry -> entry.value }] }

        // Check there are no duplicate tag declarations for the same java repo
        allTags
                .findAll { it.value.size() > 1 }
                .each {
                    throw new GradleException("Image tag '$it.key' is defined in multiple projects: $it.value")
                }

        // Check that a few common/expected tags are actually published
        println allTags.keySet().sort()

        def branchName = getBranchName(project)
        if (branchName.endsWith("release")) {
            def expectedTags = ['jdk-8', 'jdk-11']
            expectedTags.each {
                if (!allTags.containsKey(it)) {
                    throw new GradleException("Tag '$it' is expected, but not defined in any project")
                }
            }
        }
    }
}

task check {
    dependsOn(verifyImageTags)
}
